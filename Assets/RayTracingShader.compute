// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// inputs
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;

float2 _PixelOffset;

Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

static const float PI = 3.14149265f;

// output
RWTexture2D<float4> Result;


struct Ray
{
	float3 origin;
	float3 direction;
};

Ray CreateRay(float3 origin, float3 direction)
{
	Ray ray;
	ray.origin = origin;
	ray.direction = direction;
	return ray;
}

Ray CreateCameraRay(float2 uv)
{
	// camera origin to world space
	float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;

	// pixel in screen-space to camera space (on plane z = 0)
	float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
	// and to world-space as a vector.
	direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
	direction = normalize(direction);

	return CreateRay(origin, direction);
}


struct RayHit
{
	float3 position;
	float distance;
	float3 normal;
};

RayHit CreateRayHit()
{
	RayHit hit;
	hit.position = float3(0.0f, 0.0f, 0.0f);
	hit.distance = 1.#INF;
	hit.normal = float3(0.0f, 0.0f, 0.0f);
	return hit;
}

void IntersectGroundPlane(Ray ray, inout RayHit bestHit)
{
	// solve ray.origin + t * ray.direction = 0, for y = 0 plane
	float t = -ray.origin.y / ray.direction.y;
	if (t > 0.0f && t < bestHit.distance)
	{
		bestHit.distance = t;
		bestHit.position = ray.origin + t * ray.direction;
		bestHit.normal = float3(0.0f, 1.0f, 0.0f); // up world normal
	}
}

void IntersectSphere(Ray ray, inout RayHit bestHit, float4 sphere)
{
	float3 d = ray.origin - sphere.xyz;
	float p1 = -dot(ray.direction, d);
	float p2sqr = p1 * p1 - dot(d, d) + sphere.w * sphere.w;
	if (p2sqr < 0)
		return;

	float p2 = sqrt(p2sqr);
	float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
	if (t > 0.0f && t < bestHit.distance)
	{
		bestHit.distance = t;
		bestHit.position = ray.origin + t * ray.direction;
		bestHit.normal = normalize(bestHit.position - sphere.xyz);
	}
}

RayHit Trace(Ray ray)
{
	RayHit bestHit = CreateRayHit();

	IntersectGroundPlane(ray, bestHit);
	IntersectSphere(ray, bestHit, float4(0.0f, 3.0f, 0.0f, 3.0f));

	return bestHit;
}

float3 Shade(inout Ray ray, RayHit hit)
{
	// if hit 
	if (hit.distance < 1.#INF)
	{
		return 0.5f * hit.normal + 0.5f;
	}
	else // sky
	{
		// direction to spherical coordinates
		float theta = acos(ray.direction.y) / -PI;
		float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.5f;
		float4 skyColor = _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0.0f);
		return skyColor.xyz;
	}
}


[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
	uint width, height;
	Result.GetDimensions(width, height);

	// pixel center from [0,width] to [-1,1]
	float2 uv = float2(((id.xy + _PixelOffset) / float2(width, height)) * 2.0f - 1.0f);

	// get a ray from that screen-space pixel
	Ray ray = CreateCameraRay(uv);

	// Trace and shade
	RayHit hit = Trace(ray);
	float3 result = Shade(ray, hit);
	
	Result[id.xy] = float4(result, 1.0f);
}
